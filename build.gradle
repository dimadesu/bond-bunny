plugins {
    id 'com.android.application' version '8.5.2'
}

android {
    namespace 'com.dimadesu.bondbunny'
    compileSdk 35

    defaultConfig {
        applicationId "com.dimadesu.bondbunny"
        minSdk 26
        targetSdk 35
        versionCode 6
        versionName "1.10.0"
        
        ndk {
            abiFilters 'arm64-v8a', 'armeabi-v7a', 'x86', 'x86_64'
        }
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17"
            }
        }
    }

    signingConfigs {
        release {
            // Read from keystore.properties file
            def keystorePropertiesFile = rootProject.file("keystore.properties")
            if (keystorePropertiesFile.exists()) {
                def keystoreProperties = new Properties()
                keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
                
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }
    
    // Custom APK output names
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def versionName = variant.versionName
            def buildType = variant.buildType.name
            outputFileName = "Bond-Bunny-v${versionName}-${buildType}.apk"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    packaging {
        jniLibs {
            useLegacyPackaging = false
        }
    }

    // Enable NDK
    ndkVersion "25.1.8937393"
    
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.10.0'
    implementation 'androidx.localbroadcastmanager:localbroadcastmanager:1.1.0'
}

// Custom AAB output names - create copy so Android Studio can still locate original
afterEvaluate {
    tasks.named("bundleRelease").configure {
        doLast {
            def originalBundle = file("${projectDir}/release/Bond Bunny-release.aab")
            if (originalBundle.exists()) {
                def newName = "Bond-Bunny-v${android.defaultConfig.versionName}-release.aab"
                def newFile = new File(originalBundle.parent, newName)
                java.nio.file.Files.copy(originalBundle.toPath(), newFile.toPath(), 
                    java.nio.file.StandardCopyOption.REPLACE_EXISTING)
            }
        }
    }
}
